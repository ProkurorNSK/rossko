
&НаКлиенте
Перем АдресТоваров;

#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ
&НаКлиенте
Функция  ПолучитьТекущуюСтроку()
	Возврат Элементы.Товары.ТекущиеДанные;
КонецФункции

&НаСервере
Функция ПоместитьТоварыВХранилище() 
	ТаблицаТовары = Объект.Товары.Выгрузить(,"Номенклатура");
	ТаблицаТовары.Индексы.Добавить("Номенклатура");
	Возврат ПоместитьВоВременноеХранилище(ТаблицаТовары, ЭтаФорма.УникальныйИдентификатор);
КонецФункции


//Реализация на стороне Сервера без Контекстных Серверных вызовов, с использованием ВХ
//Пример другой реализации, чем в форме подбора
&НаСервереБезКонтекста
Функция ЕстьДубльТоваров(ВыбранныйТовар, АдресТоваров)
	Товары = ПолучитьИзВременногоХранилища(АдресТоваров);
	НайденныеСтрокиМассив = Товары.НайтиСтроки(Новый Структура("Номенклатура", ВыбранныйТовар));
	Возврат ?(НайденныеСтрокиМассив.Количество() = 0, Ложь, Истина);
КонецФункции
#КонецОбласти

#Область ОБРАБОТЧИКИ_КОММАНД_ФОРМЫ
//Записываем текущий список товаров во временное хранилище и передадим адрес форме подбора через параметр
&НаКлиенте
Процедура Подбор(Команда)
	АдресТоваров = ПоместитьТоварыВХранилище();
	ПараметрыФормы = Новый Структура("ЗакрыватьПриВыборе, АдресТоваров", Ложь, АдресТоваров);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры
#КонецОбласти

#Область ОБРАБОТЧИКИ_СОБЫТИЙ
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	НаКлиенте.РассчитатьСумму(ПолучитьТекущуюСтроку());
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	НаКлиенте.РассчитатьСумму(ПолучитьТекущуюСтроку());
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	НаКлиенте.РассчитатьЦену(ПолучитьТекущуюСтроку());
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Объект.Товары.Добавить().Номенклатура = ВыбранноеЗначение;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЕстьДубльТоваров(ВыбранноеЗначение, АдресТоваров) Тогда
		СтандартнаяОбработка = Ложь;
		Сообщить(СтрШаблон("Обнаружен дубль, товар ""%1"" не будет добавлен", ВыбранноеЗначение));
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	АдресТоваров = ПоместитьТоварыВХранилище();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	АдресТоваров = ПоместитьТоварыВХранилище();
КонецПроцедуры
#КонецОбласти