 
#Область ПРОЦЕДУРЫ_И_ФУНКЦИИ
//С одной стороны надо бы сделать &НаСервере, но тогда будет бессмысленно туда-сюда гонять контекст с реквизитом "Товары"
//Принял решение что лучше пусть клиент ищет строки, чем лишние Контекстные Серверные вызовы
//
//Возможен вариант без реквезита формы Товары, тогда можно всё сделать на сервере без Контекстных вызовов, с помощью ВХ Поместить/Получить
//Не знаю на сколько это будет эффективней, нужны эксперименты на приближенных к продакшн условиях, с большими данными.
//Просто для примера реализовал через ВХ для контроля дублей при добавлении сток в ТЧ без использования формы подбора
&НаКлиенте
Функция ЕстьДубльТоваров(ВыбранныйТовар)
	НайденныеСтрокиМассив = Товары.НайтиСтроки(Новый Структура("Номенклатура", ВыбранныйТовар));
	Если НайденныеСтрокиМассив.Количество() = 0 Тогда
		Товары.Добавить().Номенклатура = ВыбранныйТовар; //Побочное действие функции
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции
#КонецОбласти
 
#Область ОБРАБОТЧИКИ_СОБЫТИЙ
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.АдресТоваров) Тогда
		Товары.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресТоваров));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	Если Товары.Количество() <> 0 И ЕстьДубльТоваров(Значение) Тогда
		СтандартнаяОбработка = Ложь;
		Сообщить(СтрШаблон("Обнаружен дубль, товар ""%1"" не будет добавлен", Значение));
	КонецЕсли; 
КонецПроцедуры
#КонецОбласти